' @module BGE
namespace BGE

  class DrawablePolygon extends Drawable

    points as object = []
    drawOutline as boolean = false

    outlineRGBA as integer = BGE.Colors().White

    drawLevels as integer = 4 ' The number of frames before the polygon is completely finished

    protected tempCanvas as object = invalid
    protected lastPoints as object = []
    protected triangles as object = []

    private currentDrawLevel = 0

    protected minimumFrameRateTarget as integer = 10

    function new(owner as GameEntity, canvasBitmap as object, points = [] as object, args = {} as object) as void
      super(owner, canvasBitmap, args)
      m.points = m.cloneArray(points)
      m.append(args)
    end function

    override function draw(additionalRotation = 0 as float) as void
      if m.drawLevels < 1
        m.drawLevels = 1
      end if
      if not m.enabled
        return
      end if

      m.automaticallyAdjustDrawLevels()

      if m.pointArraysEqual(m.points, m.lastPoints) and not m.shouldRedraw
        if m.currentDrawLevel < m.drawLevels
          m.drawNextLevelOfTriangles()
        end if
        m.drawRegionToCanvas(m.tempCanvas, additionalRotation)
        return
      end if

      m.lastPoints = BGE.cloneArray(m.points)
      m.triangles = BGE.QuickHull.getTrianglesFromPoints(m.points)
      widthAndOffset = m.getMaxWidthAndHorizontalOffset(m.points)
      heightAndOffset = m.getMaxHeightAndVerticalOffset(m.points)
      m.width = widthAndOffset.width
      m.height = heightAndOffset.height
      m.offset_x = widthAndOffset.offset
      m.offset_y = heightAndOffset.offset

      m.lastWidth = m.width
      m.lastHeight = m.height
      m.currentDrawLevel = 0

      m.tempCanvas = CreateObject("roBitmap", {
        width: m.width,
        height: m.height,
        AlphaEnable: true
      })
      m.drawNextLevelOfTriangles()
      m.drawRegionToCanvas(m.tempCanvas, additionalRotation)
      m.shouldRedraw = false
    end function

    private function cloneArray(original = [] as object) as object
      retVal = []
      for each item in original
        retVal.push(item)
      end for
      return retVal
    end function

    private sub automaticallyAdjustDrawLevels()
      if m.drawLevels < 16 and (m.owner.game.getDeltaTime() * m.minimumFrameRateTarget > 1)
        m.drawLevels *= 2
      end if
    end sub

    private function pointArraysEqual(a = [] as object, b = [] as object) as boolean
      if a.count() <> b.count()
        return false
      end if

      same = true

      for i = 0 to a.count() - 1
        same = a[i].x = b[i].x and a[i].y = b[i].y
        if not same
          exit for
        end if
      end for

      return same
    end function

    private sub drawNextLevelOfTriangles()
      for each tri in m.triangles
        m.drawTriangle(tri, m.drawLevels, m.currentDrawLevel)
      end for
      m.currentDrawLevel++
    end sub


    private sub drawTriangle(tri, levelOfDetail = 1, levelOffset = 0)
      if invalid = m.tempCanvas
        return
      end if

      len0 = BGE.Math_TotalDistance(tri[1], tri[2])
      len1 = BGE.Math_TotalDistance(tri[0], tri[2])
      len2 = BGE.Math_TotalDistance(tri[0], tri[1])
      apexIndex = 0
      shortLength = 0

      if len0 <= len1 and len0 <= len2
        apexIndex = 0
        shortLength = len0
      else if len1 <= len0 and len1 < len0
        apexIndex = 1
        shortLength = len1
      else ' if len2 < len0 and len2 < len1
        apexIndex = 2
        shortLength = len2
      end if
      apex = tri[apexIndex]
      startP = tri[(apexIndex + 1) mod 3]
      endP = tri[(apexIndex + 2) mod 3]

      if shortLength > 0
        rgba = m.getFillColorRGBA()
        for i = levelOffset to shortLength step levelOfDetail
          x = BGE.LinearTween(startP.x, endP.x, i, shortLength)
          y = BGE.LinearTween(startP.y, endP.y, i, shortLength)
          m.tempCanvas.drawLine(apex.x - m.offset_x, apex.y - m.offset_y, x - m.offset_x, y - m.offset_y, rgba)
        end for
        outlineColor = rgba
        if m.drawOutline
          outlineColor = m.outlineRGBA
        end if
        m.tempCanvas.drawLine(apex.x - m.offset_x, apex.y - m.offset_y, startP.x - m.offset_x, startP.y - m.offset_y, outlineColor)
        m.tempCanvas.drawLine(apex.x - m.offset_x, apex.y - m.offset_y, endP.x - m.offset_x, endP.y - m.offset_y, outlineColor)
        m.tempCanvas.drawLine(startP.x - m.offset_x, startP.y - m.offset_y, endP.x - m.offset_x, endP.y - m.offset_y, outlineColor)
      end if
    end sub

    private function getMaxWidthAndHorizontalOffset(points) as object
      if points.count() < 2
        return 0
      end if
      minMax = BGE.QuickHull.getMinMaxPoints(points)
      return {width: minMax[1].x - minMax[0].x, offset: minMax[0].x}
    end function

    private function getMaxHeightAndVerticalOffset(points) as object
      if points.count() < 2
        return 0
      end if
      minMax = BGE.QuickHull.getMinMaxPoints(points, true)
      return {height: minMax[1].y - minMax[0].y, offset: minMax[0].y}
    end function

  end class

end namespace