class PolygonRoom extends BGE.Room

  polygonEntities = []
  polygonCount = 20
  totalEntities = 0
  drawMode = 3

  sizeConfig = {change: false, size: 100}

  polygonsLabel as BGE.UI.Label = invalid
  drawModeLabel as BGE.UI.Label = invalid

  function new(game) as void
    super(game)
    m.name = "PolygonRoom"
  end function

  override function onCreate(args)
    m.getRandomPolygons()
    m.polygonsLabel = new BGE.UI.Label(m.game)
    m.polygonsLabel.customPosition = true
    m.polygonsLabel.customX = m.game.canvas.getWidth() / 2
    m.polygonsLabel.customY = 100
    m.polygonsLabel.drawableText.alignment = "center"
    m.game.gameUi.addChild(m.polygonsLabel)
    m.drawModeLabel = new BGE.UI.Label(m.game)
    m.drawModeLabel.customPosition = true
    m.drawModeLabel.customX = m.game.canvas.getWidth() / 2
    m.drawModeLabel.customY = m.game.canvas.getHeight() - 200
    m.drawModeLabel.drawableText.alignment = "center"
    m.game.gameUi.addChild(m.drawModeLabel)
  end function


  sub getRandomPolygons()
    for each polygonEntity in m.polygonEntities
      m.game.destroyEntity(polygonEntity)
    end for
    m.polygonEntities = []
    for i = 0 to m.polygonCount - 1
      x = rnd(m.game.screen.GetWidth())
      y = rnd(m.game.screen.GetHeight())
      poly = m.game.addEntity(new Polygon(m.game), {x: x, y: y, sizeConfig: m.sizeConfig})
      m.polygonEntities.push(poly)
      m.totalEntities++
      poly.setDrawMode(m.drawMode)
    end for
  end sub

  sub changeDrawMode()
    m.drawMode = (m.drawMode + 1)
    if m.drawMode > 7
      m.drawMode = 1
    end if
    for each polygonEntity in m.polygonEntities
      polygonEntity.setDrawMode(m.drawMode)
    end for
  end sub

  override sub onInput(input)
    if not input.press
      if input.isButton("play")
        m.sizeConfig.change = not input.release
      end if
      return
    end if
    m.sizeConfig.change = false
    if input.isButton("ok")
      m.getRandomPolygons()
    else if input.isButton("options")
      m.changeDrawMode()
    else if input.isButton("up")
      m.polygonCount = int(m.polygonCount * 1.2)
      m.getRandomPolygons()
    else if input.isButton("down")
      m.polygonCount = int(m.polygonCount / 1.2)
      m.getRandomPolygons()
    end if
  end sub

  override function onGameEvent(event as string, data as object)

  end function

  override function onDrawEnd(canvas)
    m.polygonsLabel.setText(`Polygons: ${m.polygonEntities.count()}`)
    m.drawModeLabel.setText(`DrawMode: ${m.drawMode}`)
  end function


end class