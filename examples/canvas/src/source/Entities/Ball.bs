class Ball extends BGE.GameEntity

  direction = invalid
  hit_frequency_timer = CreateObject("roTimeSpan")
  dead = false
  bounds = {top: 0, left: 0, right: 1280, bottom: 720}

  function new(game as BGE.Game) as void
    super(game)
    m.name = "Ball"
  end function

  override function onCreate(args as object)
    m.direction = args.direction

    w = m.game.canvas.getWidth()
    h = m.game.canvas.getHeight()

    m.x = w / 2
    m.y = h / 2
    m.bounds.bottom = h
    m.bounds.right = w
    m.xspeed = 5.5 * m.direction
    m.yspeed = 5
    if rnd(2) = 1
      m.yspeed *= -1
    end if

    bm_ball = m.game.getBitmap("ball")
    ' bs:disable-next-line
    region = CreateObject("roRegion", bm_ball, 0, 0, bm_ball.GetWidth(), bm_ball.GetHeight())
    region.SetPreTranslation(-bm_ball.GetWidth() / 2, -bm_ball.GetHeight() / 2)
    m.addImage("main", region, {color: &hffffff, alpha: 0})
    m.addRectangleCollider("ball_collider", bm_ball.GetWidth(), bm_ball.GetHeight(), -bm_ball.GetWidth() / 2, -bm_ball.GetHeight() / 2)
    m.depth = -1
  end function


  override function onUpdate(dt as float)
    image = m.getImage("main")
    collider = m.getCollider("ball_collider")

    ' Increase alpha until full if not at full
    if image.alpha < 255
      image.alpha += 3
    end if

    ' If the ball is hitting the left bounds
    if m.x - collider.height / 2 <= m.bounds.left
      m.xspeed = abs(m.xspeed)
    end if

    ' If the ball is hitting the right bounds
    if m.x + collider.height / 2 >= m.bounds.right
      m.xspeed = abs(m.xspeed) * -1
    end if

    ' If the ball is hitting the top bounds
    if m.y - collider.height / 2 <= m.bounds.top
      m.yspeed = abs(m.yspeed)
    end if

    ' If the ball is hitting the bottom bounds
    if m.y + collider.height / 2 >= m.bounds.bottom
      m.yspeed = abs(m.yspeed) * -1
    end if
  end function



end class
