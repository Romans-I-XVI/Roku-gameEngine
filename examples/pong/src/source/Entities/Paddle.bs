class Paddle extends BGE.GameEntity

  width = invalid
  height = invalid
  bounds = {top: 50, bottom: 720 - 50}

  function new(game) as void
    super(game)
  end function

  override function onCreate(args)
    m.y = m.game.getCanvas().GetHeight() / 2

    bm_paddle = m.game.getBitmap("paddle")
    m.width = bm_paddle.GetWidth()
    m.height = bm_paddle.GetHeight()
    m.addRectangleCollider("front", m.getFrontColliderXOffset(), - m.height / 2, 1, m.height)
    m.addRectangleCollider("top", - m.width / 2, - m.height / 2, m.width - 2, 1)
    m.addRectangleCollider("bottom", - m.width / 2, m.height / 2, m.width - 2, 1)

    'bs:disable-next-line
    region = CreateObject("roRegion", bm_paddle, 0, 0, m.width, m.height)
    region.SetPreTranslation(- m.width / 2, - m.height / 2)
    m.addImage("main", region, {})
  end function

  function getFrontColliderXOffset() as float
    return - m.width / 2
  end function

end class